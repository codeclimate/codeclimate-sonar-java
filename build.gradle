apply plugin: "java"

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

task copyLibs(type: Copy) {
    into "${buildDir}/libs"
    from configurations.runtimeClasspath
    exclude "sonar-*-plugin*.jar"
}

task copyTestLibs(type: Copy) {
    into "${buildDir}/test"
    from configurations.testRuntimeClasspath
}

task copyPlugins(type: Copy) {
    into "${buildDir}/plugins"
    from configurations.runtimeClasspath
    include "sonar-*-plugin*.jar"
}

task copyRunnable(type: Copy) {
    into "${buildDir}"
    from "bin/codeclimate-sonar"
}

tasks.named("jar") {
    dependsOn copyRunnable
}

tasks.named("compileTestJava") {
    dependsOn copyRunnable
}

task infra(dependsOn: ["copyPlugins", "copyLibs", "copyTestLibs", "copyRunnable", "jar"])

build.dependsOn(infra)
test.dependsOn(infra)

dependencies {
    implementation("com.github.codeclimate:codeclimate-ss-analyzer-wrapper:beta-SNAPSHOT")

    // Plugins
    implementation("org.sonarsource.java:sonar-java-plugin:6.15.1.26025")

    testImplementation("org.assertj:assertj-core:2.8.0")
    testImplementation("org.skyscreamer:jsonassert:1.5.0")
    testImplementation("junit:junit:4.12")
}

test {
    outputs.upToDateWhen { false }
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
    }
}
